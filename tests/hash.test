# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0
## (c) 2021 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## Ankh testsuite
## - API testing
#
## ak hash HASH (string path channel)

kt check Tcl     8.5
kt check tcltest 2

kt local testing ankh

kt source support/util.tcl

# # ## ### ##### ######## ############# #####################
##

foreach hash [hashes] {
    # - -- --- ----- -------- ------------- ---------------------

    tcltest::test ankh-${hash}-string-1.0 "ak hash $hash string, wrong#args" -body {
	ak hash $hash string STR X
    } -returnCodes error -result "wrong # args: should be \"ak hash $hash string str\""

    tcltest::test ankh-${hash}-channel-1.0 "ak hash $hash channel, wrong#args" -body {
	ak hash $hash channel CHAN X
    } -returnCodes error -result "wrong # args: should be \"ak hash $hash channel chan\""

    tcltest::test ankh-${hash}-path-1.0 "ak hash $hash path, wrong#args" -body {
	ak hash $hash path PATH X
    } -returnCodes error -result "wrong # args: should be \"ak hash $hash path path\""

    # - -- --- ----- -------- ------------- ---------------------

    # Fixed set of test vectors collected from the various digests.
    foreach {label vector} [vectors] {
	incr k

	if {$label eq {}} { set label $vector }
	set digest [cat [td]/results/$hash/$label]
	set tmp         [td]/in.[string map {/ _} $hash].$label
	write $tmp $vector

	tcltest::test ankh-${hash}-string-2.${k}-$label "ak hash $hash string, $label" -body {
	    binary encode hex [ak hash $hash string $vector]
	} -result $digest

	tcltest::test ankh-${hash}-channel-2.${k}-$label "ak hash $hash channel, $label" -setup {
	    set chan [open $tmp rb]
	} -body {
	    binary encode hex [ak hash $hash channel $chan]
	} -cleanup {
	    close $chan
	    unset chan
	} -result $digest

	tcltest::test ankh-${hash}-path-2.${k}-$label "ak hash $hash path, $label" -body {
	    binary encode hex [ak hash $hash path $tmp]
	} -result $digest

	file delete $tmp
    }
    unset k

    # - -- --- ----- -------- ------------- ---------------------

    # Block vectors 'a'*k, k in {0, ...e, 149}
    set k -1
    foreach digest [split [cat [td]/results/$hash/blocks] \n] {
	if {$digest eq {}} continue

	incr k
	set vector  [string repeat a $k]
	set vecfile [write [td]/[pid] $vector]

	tcltest::test ankh-${hash}-string-3.${k} "ak hash $hash string, a*$k" -body {
	    binary encode hex [ak hash $hash string $vector]
	} -result $digest

	tcltest::test ankh-${hash}-channel-3.${k} "ak hash $hash channel, a*$k" -body {
	    binary encode hex [ak hash $hash channel [open $vecfile rb]]
	} -result $digest

	tcltest::test ankh-${hash}-path-3.${k} "ak hash $hash path, a*$k" -body {
	    binary encode hex [ak hash $hash path $vecfile]
	} -result $digest

	file delete $vecfile
	unset vector vecfile
    }

    # - -- --- ----- -------- ------------- ---------------------
    unset k
}

# # ## ### ##### ######## ############# #####################
cleanupTests
return
